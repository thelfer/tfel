/*! 
 * \file   castemmieheapellambrechtlogarithmicstrain-uniaxialloading-1-2D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  4 dec. 2013
 */

@ModellingHypothesis 'Axisymmetrical';
@StiffnessMatrixType 'ConsistentTangentOperator';
@MaximumNumberOfSubSteps 1;

@Behaviour<umat> '@MFrontCastemBehavioursBuildPath@' 'umatlogarithmicstrainelasticity';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FRR' {0:1.,1.:1.8};
@ImposedDrivingVariable 'FRZ' 0.;
@ImposedDrivingVariable 'FZR' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 15};

@Evolution<function> 'err' 'log(FRR**2)/2';
@Evolution<function> 'ezz' '-PoissonRatio*err';
@Evolution<function> 'ett' '-PoissonRatio*err';
@Evolution<function> 'J'   'sqrt(exp(2*ett)*exp(2*ezz))*FRR';
@Evolution<function> 'Trr' 'YoungModulus*err';

@Test<function> {'SZZ':'0','STT':'0'} 1.e-3;
@Test<function> {'SRR':'Trr/J'}       1.e-3;
@Test<function> {'SRZ':'0'}           1.e-4;
@Test<function> {'FTT': 'sqrt(exp(2*ett))','FZZ': 'sqrt(exp(2*ezz))'} 1.e-10;
@Test<function> {'FRZ': '0', 'FZR':'0'} 1.e-10;



