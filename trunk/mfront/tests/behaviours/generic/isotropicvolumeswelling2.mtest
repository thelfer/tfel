@Author Helfer Thomas;
@Date   11/08/2016;
@Description{
  "This test checks that an isotropic swelling is correctly "
  "taken into account."
};

@Real 'tmax' '3600*24*365';

@XMLOutputFile @xml_output@;
@MaximumNumberOfSubSteps 1;
@Behaviour<generic> @library@ 'IsotropicVolumeSwelling2';

@MaterialProperty<constant> 'YoungModulus' 150.e9;
@MaterialProperty<constant> 'PoissonRatio'   0.3;

@ExternalStateVariable 'Temperature'   1200.;
@ExternalStateVariable 'BurnUp_AtPercent' {0:0,'tmax':5};

@ImposedStrain 'EXX' 0.;
@ImposedStrain 'EYY' 0.;
@ImposedStrain 'EZZ' 0.;

@Times {0.,'tmax' in 20};

@Evolution<function> 's' '-9.e-3*(1-exp(-3.e-2*(1+exp(-750/Temperature))*BurnUp_AtPercent))';

@Test<function> 'SXX' '-YoungModulus/(1-2*PoissonRatio)*s/3'  1.e-3;
@Test<function> 'SYY' '-YoungModulus/(1-2*PoissonRatio)*s/3'  1.e-3;
@Test<function> 'SZZ' '-YoungModulus/(1-2*PoissonRatio)*s/3'  1.e-3;
@Test<function> {'Shrinkage':'s'} 1.e-12;
@Test<function> {'EXY':'0.','EXZ':'0.','EYZ':'0.'} 1.e-12;
@Test<function> {'SXY':'0.','SXZ':'0.','SYZ':'0.'} 1.e-2;
