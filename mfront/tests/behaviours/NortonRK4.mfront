@DSL RungeKutta;
@Behaviour NortonRK4;
@Author    Helfer Thomas;
@Algorithm rk42;
@Date 23/11/06;

@Epsilon 1.e-8;

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@MaterialProperty real E[100]; /* Norton exponant      */

@LocalVariable real lambda; /* first  Lame coefficient  */
@LocalVariable real mu;     /* second Lame coefficient */

@StateVariable real    p[100];   /* Equivalent viscoplastic strain */
@StateVariable Stensor evp[100]; /* Viscoplastic strain            */

@ExternalStateVariable real A[100]; /* Norton coefficient      */

/* Initialize Lame coefficients */
@InitLocalVariables{
  lambda = computeLambda(young,nu);
  mu = computeMu(young,nu);
}

@PredictionOperator{
  if(!((smt==ELASTIC)||(smt==SECANTOPERATOR)||(smt==TANGENTOPERATOR))){
    return FAILURE;
  }
  Dt=lambda*Stensor4::IxI()+2*mu*Stensor4::Id();
}

@IsTangentOperatorSymmetric true;
@TangentOperator{
  if(!((smt==ELASTIC)||(smt==SECANTOPERATOR)||(smt==TANGENTOPERATOR))){
    return FAILURE;
  }
  Dt=lambda*Stensor4::IxI()+2*mu*Stensor4::Id();
}

@ComputeStress{
  sig = lambda*trace(eel)*StrainStensor::Id()+2*mu*eel;
}

@Derivative{
  constexpr real cste = real{3}/real{2};
  const real sigeq = sigmaeq(sig);
  if(sigeq>1.e9){
    return false;
  }
  const Stensor n = (sigeq > 10.e-7) ? cste*deviator(sig)/sigeq : Stensor(0);
  deel = deto;
  for(unsigned short i=0;i!=evp.size();++i){
    dp[i]    = (A[i]+dA[i])*pow(sigeq,E[i]);
    devp[i]  = dp[i]*n;
    deel    -= devp[i];
  }
}
