/*! 
 * \file   cyranoimplicitnorton.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  02 avril 2014
 */

@Description{
  "Test the generic plane stress"
  "algorithm of the umat interface."
  "This is much less efficient than"
  "directly supporting plane stress"
  "within the behaviour."
 };

@CompareToNumericalTangentOperator true;
@TangentOperatorComparisonCriterium 3e6;
@NumericalTangentOperatorPerturbationValue 1.e-8;

@MaximumNumberOfSubSteps   1;

@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStress';
@Behaviour<cyrano> '@MFrontCyranoBehavioursBuildPath@' 'cyranoimplicitnorton';

@MaterialProperty<constant> 'YoungModulus'     150.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@Real 'srr' 20.e6;
@ImposedStress 'SRR' 'srr';
// Initial value of the elastic strain
@Real 'EELRR0'  0.00013333333333333333;
@Real 'EELZZ0' -0.00004;
@InternalStateVariable 'ElasticStrain' {'EELRR0','EELZZ0','EELZZ0'};
@InternalStateVariable 'AxialStrain'   'EELZZ0';
// Initial value of the total strain
@Strain {'EELRR0',0.,'EELZZ0'};
// Initial value of the stresses
@Stress {'srr',0.,0.};

@ExternalStateVariable 'Temperature' 293.15;
@ExternalStateVariable 'AxialStress' 0.;

@Times {0.,3600 in 20};

// tests on strains
// note: p is known at 1.e-12 (defaut value
// for @StrainEpsilon), thus we may expect the strain to be known at
// '3.6*1.e-9'. If pratice, things are a bit better but not much
// better.
@Real 'A' 8.e-67;
@Real 'E' 8.2;
@Test<function> 'ERR' 'EELRR0+A*SRR**E*t'     1.e-9;
@Test<function> 'ETT' 'EELZZ0-0.5*A*SRR**E*t' 1.e-10;
@Test<function> 'EZZ' '0.'                    1.e-10;
@Test<function> 'AxialStrain' 'EELZZ0-0.5*A*SRR**E*t' 1.e-10;
// tests on internal state variables
@Test<function> 'ElasticStrainRR' 'EELRR0'  1.e-12;
@Test<function> 'ElasticStrainTT' 'EELZZ0'  1.e-12;
@Test<function> 'ElasticStrainZZ' 'EELZZ0'  1.e-12;
@Test<function> 'p' 'A*SRR**E*t' 1.e-12;
// this test is a bit paranoiac since SRR is imposed
@Test<function> 'SRR' 'SRR'     1.e-3;
// check that the mechanical equilibrium is satisfied
@Test<function> 'STT' '0.'      1.e-3;
@Test<function> 'STT' '0.'      1.e-3;
