@Author Helfer Thomas;
@Date  29/02/2016;
@Description{
  "This test checks that the stiffness tensor "
  "is correctly computed for an "
  "orthotropic material."
};

@MaximumNumberOfSubSteps 1;
@AccelerationAlgorithm 'Cast3M';
@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStress';
@Behaviour<cyrano> '$<TARGET_FILE:MFrontCyranoBehaviours>' 'cyranoorthotropicelastic6';

@ExternalStateVariable 'Temperature' 293.15;
@ExternalStateVariable 'AxialStress' 0;

@ImposedStrain<function> 'ETT' '1.e-3*sin(t/900.)';

@Times {0.,3600 in 20};

@Real 'E1'   '7.8e+10';
@Real 'E2'   '2.64233e+11';
@Real 'E3'   '3.32e+11';
@Real 'nu12' '0.13';
@Real 'nu13' '0.18';
@Real 'nu23' '0.24';
// stresses
@Test<function> 'STT' 'E3*ETT'  1.e-3;
@Test<function> {'SRR':'0','SZZ':'0'}  1.e-3;
// strains
@Test<function> 'ERR' '-nu13*(E3/E1)*ETT'  1.e-12;
@Test<function> 'EZZ' '0'  1.e-12;
