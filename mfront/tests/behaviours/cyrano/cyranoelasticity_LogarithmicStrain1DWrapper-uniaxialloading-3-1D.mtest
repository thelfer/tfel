/*! 
 * \file   cyranoelasticity_malls-uniaxialloading-3-1D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  11 dec. 2014
 */

@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStrain';
@Behaviour<cyrano,LogarithmicStrain1D> @library@ 'cyranoelasticity';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'ETT' {0:0.,1.:0.8};

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Evolution<function> 'FTT' '1+ETT';
@Evolution<function> 'ett' 'log(FTT**2)/2';
@Evolution<function> 'err' '-PoissonRatio*ett';
@Evolution<function> 'ezz' '-PoissonRatio*ett';
@Evolution<function> 'J'   'sqrt(exp(2*err)*exp(2*ezz))*FTT';
@Evolution<function> 'Ttt' 'YoungModulus*ett';

@Test<function> {'SRR':'0','SZZ':'0'} 1.e-3;
@Test<function> {'STT':'Ttt/(1+ETT)'} 1.e-3;
@Test<function> {'ERR': 'sqrt(exp(2*err))-1','EZZ': 'sqrt(exp(2*ezz))-1'} 1.e-10;
