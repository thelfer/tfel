configure_file("${PROJECT_SOURCE_DIR}/mfront/tests/behaviours/references/PolyCrystalsAngles.txt"
    PolyCrystalsAngles.txt @ONLY)

function(test_crossed2deltabis test_arg)
  set(_NO_XML_OUTPUT )
  set(_WILL_FAIL )
  set(_EXPECT_ADDITIONAL_ENVIRONMENT )
  set(_ADDITIONAL_ENVIRONMENT )
  set(_EXPECT_REFERENCE_FILE)
  set(_REFERENCE_FILE )
  foreach(_ARG ${ARGN})
    if(_EXPECT_ADDITIONAL_ENVIRONMENT)
      if(_ADDITIONAL_ENVIRONMENT)
	message(FATAL_ERROR "test_crossed2deltabis: environment already set")
      endif(_ADDITIONAL_ENVIRONMENT)
      set(_ADDITIONAL_ENVIRONMENT "${_ARG}")
      set(_EXPECT_ADDITIONAL_ENVIRONMENT )
    elseif(_EXPECT_REFERENCE_FILE)
      if(_REFERENCE_FILE)
	message(FATAL_ERROR "test_crossed2deltabis: reference file already set")
      endif(_REFERENCE_FILE)
      set(_REFERENCE_FILE "${_ARG}")
      set(_EXPECT_REFERENCE_FILE )
    else()
      if ( ${_ARG} MATCHES NO_XML_OUTPUT )
	set ( _NO_XML_OUTPUT ON)
      elseif ( ${_ARG} MATCHES WILL_FAIL)
	set ( _WILL_FAIL ON)
      elseif ( ${_ARG} MATCHES ENVIRONMENT)
	set(_EXPECT_ADDITIONAL_ENVIRONMENT ON)
      elseif ( ${_ARG} MATCHES REFERENCE_FILE)
	set(_EXPECT_REFERENCE_FILE ON)
      else()
	message(FATAL_ERROR "test_crossed2deltabis: unsupported option '${_ARG}'")
      endif()
    endif()
  endforeach(_ARG ${ARGN})
  if(_EXPECT_ADDITIONAL_ENVIRONMENT)
    message(FATAL_ERROR "test_crossed2deltabis: expected environment variable")
  endif()
  if(_EXPECT_REFERENCE_FILE)
    message(FATAL_ERROR "test_crossed2deltabis: expected reference file")
  endif()
  if(_REFERENCE_FILE)
    set(_REFERENCE_FILE "${PROJECT_SOURCE_DIR}/mfront/tests/behaviours/references/${_REFERENCE_FILE}")
  endif(_REFERENCE_FILE)
  set(test "crossed2deltabis${test_arg}_mtest")
  set(file "${CMAKE_CURRENT_SOURCE_DIR}/crossed2deltabis${test_arg}.mtest")
  set(_mtest_args)
  list(APPEND _mtest_args --verbose=level0)
  list(APPEND _mtest_args --result-file-output=false)
  list(APPEND _mtest_args --@interface@=umat)
  list(APPEND _mtest_args --@library@="$<TARGET_FILE:MFrontCastemBehaviours>")
  list(APPEND _mtest_args --@top_srcdir@=${PROJECT_SOURCE_DIR})
  list(APPEND _mtest_args --@mplibrary@="$<TARGET_FILE:MFrontMaterialProperties-castem>")
  list(APPEND _mtest_args --@reference_file@="${_REFERENCE_FILE}")
  if(_NO_XML_OUTPUT)
    list(APPEND _mtest_args --xml-output=false)
  else(_NO_XML_OUTPUT)
    list(APPEND _mtest_args --xml-output=true)
  endif(_NO_XML_OUTPUT)
  add_test(NAME ${test} COMMAND mtest ${_mtest_args} ${file})
  if(_WILL_FAIL)
    set_tests_properties(${test} PROPERTIES WILL_FAIL true)
  endif(_WILL_FAIL)
  if((CMAKE_HOST_WIN32) AND (NOT MSYS))
    set_property(TEST ${test}        
       PROPERTY ENVIRONMENT ${_ADDITIONAL_ENVIRONMENT}
      "PATH=$<TARGET_FILE_DIR:TFELMTest>\;$<TARGET_FILE_DIR:TFELMFront>\;$<TARGET_FILE_DIR:MFrontLogStream>\;$<TARGET_FILE_DIR:TFELMaterial>\;$<TARGET_FILE_DIR:TFELMathParser>\;$<TARGET_FILE_DIR:TFELGlossary>\;$<TARGET_FILE_DIR:TFELSystem>\;$<TARGET_FILE_DIR:TFELUtilities>\;$<TARGET_FILE_DIR:TFELException>\;$<TARGET_FILE_DIR:TFELTests>\;$<TARGET_FILE_DIR:TFELConfig>\;$ENV{PATH}")
  else((CMAKE_HOST_WIN32) AND (NOT MSYS))
    if(_ADDITIONAL_ENVIRONMENT)
      set_property(TEST ${test} PROPERTY ENVIRONMENT ${_ADDITIONAL_ENVIRONMENT})
    endif(_ADDITIONAL_ENVIRONMENT)
  endif((CMAKE_HOST_WIN32) AND (NOT MSYS))
  if(TFEL_APPEND_VERSION)
    install(FILES ${file}
      DESTINATION "share/doc/mfront-${TFEL_VERSION}/tests/behaviours/crossed2deltabis"
      COMPONENT mtest)
  else(TFEL_APPEND_VERSION)
    install(FILES ${file}
      DESTINATION "share/doc/mfront/tests/behaviours/crossed2deltabis"
      COMPONENT mtest)
  endif(TFEL_APPEND_VERSION)
endfunction(test_crossed2deltabis)

test_crossed2deltabis(ddif2-2)
test_crossed2deltabis(ddif2-3)
test_crossed2deltabis(ddif2-4)
test_crossed2deltabis(ddif2)
test_crossed2deltabis(druckerprager)
test_crossed2deltabis(ellipticcreep)
test_crossed2deltabis(finitestrainsinglecrystal-001)
test_crossed2deltabis(finitestrainsinglecrystal-012-2)
test_crossed2deltabis(finitestrainsinglecrystal-012)
test_crossed2deltabis(finitestrainsinglecrystal-159)
test_crossed2deltabis(finitestrainsinglecrystal2-001)
test_crossed2deltabis(finitestrainsinglecrystal_numericaljacobian-001)
test_crossed2deltabis(finitestrainsinglecrystal_numericaljacobian-012-2)
test_crossed2deltabis(finitestrainsinglecrystal_numericaljacobian-012)
test_crossed2deltabis(finitestrainsinglecrystal_numericaljacobian-159)
test_crossed2deltabis(hayhurst)
test_crossed2deltabis(implicithayhurst)
test_crossed2deltabis(implicitmonocrystal)
test_crossed2deltabis(implicitnorton3)
#test_crossed2deltabis(implicitnorton-planestress2)
test_crossed2deltabis(lorentz)
test_crossed2deltabis(monocrystal_dd_cc_irradiation)
test_crossed2deltabis(monocrystal_dd_cc_irradiation_numericaljacobian)
test_crossed2deltabis(monocrystal_dd_cfc_irradiation)
test_crossed2deltabis(monocrystal_dd_cfc)
test_crossed2deltabis(monocrystal_dd_cfc_numericaljacobian)
test_crossed2deltabis(monocrystal)
test_crossed2deltabis(norton2)
test_crossed2deltabis(norton-euler)
test_crossed2deltabis(norton)
test_crossed2deltabis(norton-rk2)
test_crossed2deltabis(nortonrk2)
test_crossed2deltabis(nortonrk3)
test_crossed2deltabis(norton-rk4)
test_crossed2deltabis(nortonrk4)
test_crossed2deltabis(plasisocin)
test_crossed2deltabis(plasticity)
test_crossed2deltabis(strainhardeningcreep)
test_crossed2deltabis(stressrelaxation)
# test_crossed2deltabis(T91-satohtest)
test_crossed2deltabis(T91-viscositytest)
test_crossed2deltabis(viscoplasisocin)

# test_crossed2deltabis(mieheapellambrechtlogarithmicstrainnortoncreep-uniaxialloading-3-3D)
# test_crossed2deltabis(monocrystal_dd_cc)
# test_crossed2deltabis(monocrystal_dd_cc_numericaljacobian)
