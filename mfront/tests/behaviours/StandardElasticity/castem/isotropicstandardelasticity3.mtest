@Author  Thomas Helfer;
@Date 6/05/2016;
@Description{
  This file describes a compressive test along XX with
  a constant loading.
  The Young modulus varies with the temperature
  during the test.
  Simulation Results are compared to the
  analytical solution.
};

@XMLOutputFile @xml_output@;
@MaximumNumberOfSubSteps 1;
@Behaviour<umat> @library@ @behaviour@;
// The TC evolution is defined below
@MaterialProperty<function> 'YoungModulus' '(-3.1636e-3*TC*TC-3.8654*TC+2.1421e+4)*1e7';
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@Real 'T0' 400.;
@Real 'T1' 523.;
@ExternalStateVariable<function> 'Temperature' '(T1-T0)*sin(t/900.)+T0';
// an helper function of time defining the temperature in Celsius
@Evolution<function> 'TC' 'Temperature-273.15';

@Real 'sxx' 50.e6;
@ImposedStress 'SXX' 'sxx';
// Initial value of the total strains
@Strain {0.000239466253465591,
         -7.18398760396772e-05,
         -7.18398760396772e-05,
         0.,0.,0.};
@InternalStateVariable 'ElasticStrain' { 0.000239466253465591,
                                        -7.18398760396772e-05,
                                        -7.18398760396772e-05,
                                         0.,0.,0.};
// Initial value of the stresses
@Stress {'sxx',0.,0.,0.,0.,0.};

@Times {0.,3600 in 20};

@Test<function> {'EXX':'SXX/YoungModulus',
                 'EYY':'-PoissonRatio*SXX/YoungModulus',
                 'EZZ':'-PoissonRatio*SXX/YoungModulus',
                 'EXY':'0.','EXZ':'0.','EYZ':'0.'} 1.e-12;
