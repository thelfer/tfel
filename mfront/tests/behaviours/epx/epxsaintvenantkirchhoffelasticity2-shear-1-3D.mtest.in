/*! 
 * \file    SaintVenantKirchhoffElasticity2-shear-1-3D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  26 nov. 2013
 */

@AccelerationAlgorithm 'Cast3M';
@Behaviour<epx> '$<TARGET_FILE:MFrontEuroplexusBehaviours>' 'SaintVenantKirchhoffElasticity2';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FXX' 1.;
@ImposedDrivingVariable 'FYY' 1.;
@ImposedDrivingVariable 'FZZ' 1.;
@ImposedDrivingVariable 'FXY' {0:0.,1.:0.2};
@ImposedDrivingVariable 'FYX' 0.;
@ImposedDrivingVariable 'FXZ' 0.;
@ImposedDrivingVariable 'FZX' 0.;
@ImposedDrivingVariable 'FYZ' 0.;
@ImposedDrivingVariable 'FZY' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Real 'lambda' 'PoissonRatio*YoungModulus/((1+PoissonRatio)*(1-2*PoissonRatio))';
@Real 'mu'     'YoungModulus/(2*(1+PoissonRatio))';
@Test<function> {'SXX':'((2*mu+lambda)*FXY**4+(4*mu+lambda)*FXY**2)/2'} 1.e-2;
@Test<function> {'SYY':'((2*mu+lambda)*FXY**2)/2'} 1.e-2;
@Test<function> {'SZZ':'(lambda*FXY**2)/2 '} 1.e-2;
@Test<function> {'SXY':'sqrt(2)*((2*mu+lambda)*FXY**3+2*mu*FXY)/2'} 1.e-2;
@Test<function> {'SXZ':'0.','SYZ':'0.'} 1.e-2;

