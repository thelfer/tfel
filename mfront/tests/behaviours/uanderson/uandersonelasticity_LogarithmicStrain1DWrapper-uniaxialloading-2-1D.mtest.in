/*! 
 * \file   castemelasticity_LogarithmicStrain1DWrapper-uniaxialloading-2-1D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  11 dec. 2013
 */

@AccelerationAlgorithm 'UAnderson';
@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStrain';
@Behaviour<castem,LogarithmicStrain1D> '$<TARGET_FILE:MFrontCastemBehaviours>' 'umatelasticity';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'EZZ' {0:0.,1.:0.8};

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Evolution<function> 'FZZ' '1+EZZ';
@Evolution<function> 'ezz' 'log(FZZ**2)/2';
@Evolution<function> 'err' '-PoissonRatio*ezz';
@Evolution<function> 'ett' '-PoissonRatio*ezz';
@Evolution<function> 'J'   'sqrt(exp(2*err)*exp(2*ett))*FZZ';
@Evolution<function> 'Tzz' 'YoungModulus*ezz';

@Test<function> {'SRR':'0','STT':'0'} 1.e-3;
@Test<function> {'SZZ':'Tzz/(1+EZZ)'} 1.e-3;
@Test<function> {'ERR': 'sqrt(exp(2*err))-1','ETT': 'sqrt(exp(2*ett))-1'} 1.e-10;
