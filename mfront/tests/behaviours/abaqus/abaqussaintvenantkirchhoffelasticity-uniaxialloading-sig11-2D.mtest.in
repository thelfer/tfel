@Author T. Helfer;
@Date 15/09/2015;
@Description{
  "This test describes an uniaxial loading "
  "by imposing the deformation gradient in "
  "one direction. "
  "The perpendicular directions are "
  "stress-free. The shear components of the "
  "deformation gradient are set to zero. "
  "The results are compared to analytical solutions."
};

@ModellingHypothesis 'Axisymmetrical';
@Behaviour<abaqus> '@MFrontAbaqusBehavioursBuildPath@' 'SAINTVENANTKIRCHHOFFELASTICITY';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FZZ' {0:1.,1.:1.2};
@ImposedDrivingVariable 'FRZ' 0.;
@ImposedDrivingVariable 'FZR' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Test<function> {'SZZ':'YoungModulus*(FZZ**2-1)*FZZ/(2*(1-PoissonRatio*(FZZ**2-1)))'} 1.e-2;
@Test<function> {'SRZ':'0'} 1.e-2;

@Test<function> {'FRR':'sqrt(1-PoissonRatio*(FZZ**2-1))'} 1.e-8;
@Test<function> {'FTT':'sqrt(1-PoissonRatio*(FZZ**2-1))'} 1.e-8;

