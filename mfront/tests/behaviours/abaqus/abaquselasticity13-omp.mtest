@Author Helfer Thomas;
@Date  09 avril 2013;
@Description{
  "This test checks that thermal expansion are correctly taken into "
  "account for an orthotropic material."
};

@ModellingHypothesis 'Axisymmetrical';

@Behaviour<abaqus> @library@ 'ABAQUSORTHOTROPICELASTIC_AXIS';

@Real 'E1'   '7.8e+10';
@Real 'E2'   '2.64233e+11';
@Real 'E3'   '2.64233e+11';
@Real 'nu12' '0.0039991';
@Real 'nu13' '0.0039991';
@Real 'nu23' '0.00736051';
@Real 'G12'  '4.8e+10';
@Real 'a1'   '3.41e-5';
@Real 'a2'   '2.23e-5';
@Real 'a3'   '4.63e-5';

@MaterialProperty<constant> 'YoungModulus1'     'E1';
@MaterialProperty<constant> 'YoungModulus2'     'E2';
@MaterialProperty<constant> 'YoungModulus3'     'E3';
@MaterialProperty<constant> 'PoissonRatio12'    'nu12';
@MaterialProperty<constant> 'PoissonRatio13'    'nu13';
@MaterialProperty<constant> 'PoissonRatio23'    'nu23';
@MaterialProperty<constant> 'ShearModulus12'    'G12';
@MaterialProperty<constant> 'ThermalExpansion1' 'a1';
@MaterialProperty<constant> 'ThermalExpansion2' 'a2';
@MaterialProperty<constant> 'ThermalExpansion3' 'a3';

@ExternalStateVariable 'Temperature' {0:293.15,3600.:800};
@Real 'ThermalExpansionReferenceTemperature' 293.15;
@Real 'Tref' 'ThermalExpansionReferenceTemperature';

@ImposedStrain 'ERR' 0.;
@ImposedStrain 'EZZ' 0.;
@ImposedStrain 'ETT' 0.;
@ImposedStrain 'ERZ' 0.;

@Real 'pi'   '4*atan(1)';
@Real 'phi'  'pi/7';
@Real 'c'    'cos(phi)';
@Real 's'    'sin(phi)';
@RotationMatrix {{'c' ,'s',0},
                 {'-s','c',0},
		 {0,0,1}};

@Times {0.,3600 in 20};

@Real 'S11' '1/E1';
@Real 'S22' '1/E2';
@Real 'S33' '1/E3';
@Real 'S12' '-nu12/E1';
@Real 'S13' '-nu13/E1';
@Real 'S23' '-nu23/E2';
@Real 'dS'  'S11*S22*S33+2*S23*S13*S12-S11*S23*S23-S22*S13*S13-S33*S12*S12';
@Real 'C00' '(S22*S33-S23*S23)/dS';
@Real 'C01' '(S13*S23-S12*S33)/dS';
@Real 'C02' '(S12*S23-S13*S22)/dS';
@Real 'C10' '(S13*S23-S12*S33)/dS';
@Real 'C11' '(S11*S33-S13*S13)/dS';
@Real 'C12' '(S12*S13-S11*S23)/dS';
@Real 'C20' '(S12*S23-S13*S22)/dS';
@Real 'C21' '(S12*S13-S11*S23)/dS';
@Real 'C22' '(S11*S22-S12*S12)/dS';

@Real 'sig11' '-(C00*a1+C01*a2+C02*a3)';
@Real 'sig22' '-(C10*a1+C11*a2+C12*a3)';

@Test<function> 'SRR' '(c**2*sig11+s**2*sig22)*(Temperature-Tref)'    1.e-3;
@Test<function> 'SZZ' '(s**2*sig11+c**2*sig22)*(Temperature-Tref)'    1.e-3;
@Test<function> 'SRZ' 'sqrt(2)*s*c*(-sig11+sig22)*(Temperature-Tref)' 1.e-3;
@Test<function> 'STT' '-(C20*a1+C21*a2+C22*a3)*(Temperature-Tref)'    1.e-3;
