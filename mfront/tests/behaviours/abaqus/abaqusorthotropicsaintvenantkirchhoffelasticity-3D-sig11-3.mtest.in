@Author Thomas Helfer;
@Date   15 sept. 2016;
@Description{
  A tensile test on an orthotropic version of
  the Saint-Venant Kirchhoff behaviour.
};

@MaximumNumberOfSubSteps 1;
@Behaviour<abaqus> '$<TARGET_FILE:MFrontAbaqusBehaviours>' 'ORTHOTROPICSAINTVENANTKIRCHHOFFELASTICITY_3D';
@StressEpsilon 1;

@ImposedDeformationGradient 'FXX' {0.:1.,1.:1.4};
@ImposedDeformationGradient 'FXY' 0;
@ImposedDeformationGradient 'FYX' 0;
@ImposedDeformationGradient 'FXZ' 0;
@ImposedDeformationGradient 'FZX' 0;
@ImposedDeformationGradient 'FYZ' 0;
@ImposedDeformationGradient 'FZY' 0;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0,1. in 10};

@Real 'YoungModulus1'  '7.8e+10';
@Real 'YoungModulus2'  '2.64233e+11';
@Real 'YoungModulus3'  '3.32e+11';
@Real 'PoissonRatio12' '0.13';
@Real 'PoissonRatio23' '0.24';
@Real 'PoissonRatio13' '0.18';

@RotationMatrix {{0,0,1},
                 {0,1,0},
		 {1,0,0}};

@Evolution<function> 'Fxx' 'FXX';
@Evolution<function> 'Exx' '(Fxx**2-1)/2';
@Evolution<function> 'Eyy' '-PoissonRatio23*YoungModulus3/YoungModulus2*Exx';
@Evolution<function> 'Fyy' 'sqrt(1+2*Eyy)';
@Evolution<function> 'Ezz' '-PoissonRatio13*YoungModulus3/YoungModulus1*Exx';
@Evolution<function> 'Fzz' 'sqrt(1+2*Ezz)';

@Test<function> 'SXX' 'YoungModulus3*Exx*Fxx/(Fzz*Fyy)' 1.e-2;
@Test<function> 'FYY' 'Fyy' 1.e-11;
@Test<function> 'FZZ' 'Fzz' 1.e-11;
