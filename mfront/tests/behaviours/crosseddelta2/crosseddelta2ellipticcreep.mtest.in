/*! 
 * \file   ellipticcreep.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  09 avril 2013
 */

@MaximumNumberOfSubSteps 1;
@AccelerationAlgorithm 'CrossedDelta2';

@ModellingHypothesis 'Axisymmetrical';

@Behaviour<umat> '@MFrontCastemBehavioursBuildPath@' 'umatellipticcreep';
@StrainEpsilon 1.e-8;
@StressEpsilon '150e9*1.e-8';
@MaterialProperty<constant> 'YoungModulus'     150.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@InternalStateVariable 'Porosity' 0.;

@Real 'srr' -20.e6;
@ImposedStress 'SRR' 'srr';
// Initial value of the elastic strain
@Real 'EELRR0' 'srr/YoungModulus';
@Real 'EELZZ0' '-EELRR0*PoissonRatio';
@InternalStateVariable 'ElasticStrain' {'EELRR0','EELZZ0','EELZZ0',0.};
// Initial value of the total strain
@Strain {'EELRR0','EELZZ0','EELZZ0',0.};
// Initial value of the stresses
@Stress {'srr',0.,0.,0.};

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,3600 in 20};

// tests on strains
@Real 'A' 8.e-67; 
@Real 'E' 8.2; 
@Test<function> 'ERR' 'EELRR0-A*SRR**E*t'     1.e-8;
@Test<function> 'EZZ' 'EELZZ0+0.5*A*SRR**E*t' 1.e-8;
@Test<function> 'ETT' 'EELZZ0+0.5*A*SRR**E*t' 1.e-8;
@Test<function> 'ERZ' '0.'                    1.e-8;
// tests on internal state variables
@Test<function> 'ElasticStrainRR' 'EELRR0'  1.e-8;
@Test<function> 'ElasticStrainZZ' 'EELZZ0'  1.e-8;
@Test<function> 'ElasticStrainTT' 'EELZZ0'  1.e-8;
@Test<function> 'ElasticStrainRZ' '0.'      1.e-8;
// @Test<function> 'EquivalentViscoplasticStrain' 'A*SRR**E*t' 1.e-12;
// this test is a bit paranoiac since SRR is imposed
@Test<function> 'SRR' 'SRR'     1.2e3;
// check that the mechanical equilibrium is satisfied
@Test<function> 'SZZ' '0.'      1.e3;
@Test<function> 'STT' '0.'      1.e3;
@Test<function> 'SRZ' '0.'      1.;
@Test<function> 'DeviatoricEquivalentViscoplasticStrain'  'A*SRR**E*t'  1.e-10;
@Test<function> 'HydrostaticEquivalentViscoplasticStrain' '0'  1.e-10;
@Test<function> 'Porosity' '0'  1.e-10;
