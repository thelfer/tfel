configure_file("${PROJECT_SOURCE_DIR}/mfront/tests/behaviours/references/PolyCrystalsAngles.txt"
    PolyCrystalsAngles.txt @ONLY)

function(test_crosseddelta2 test_arg)
  set(_NO_XML_OUTPUT )
  set(_WILL_FAIL )
  set(_EXPECT_ADDITIONAL_ENVIRONMENT )
  set(_ADDITIONAL_ENVIRONMENT )
  set(_EXPECT_REFERENCE_FILE)
  set(_REFERENCE_FILE )
  foreach(_ARG ${ARGN})
    if(_EXPECT_ADDITIONAL_ENVIRONMENT)
      if(_ADDITIONAL_ENVIRONMENT)
	message(FATAL_ERROR "test_crosseddelta2: environment already set")
      endif(_ADDITIONAL_ENVIRONMENT)
      set(_ADDITIONAL_ENVIRONMENT "${_ARG}")
      set(_EXPECT_ADDITIONAL_ENVIRONMENT )
    elseif(_EXPECT_REFERENCE_FILE)
      if(_REFERENCE_FILE)
	message(FATAL_ERROR "test_crosseddelta2: reference file already set")
      endif(_REFERENCE_FILE)
      set(_REFERENCE_FILE "${_ARG}")
      set(_EXPECT_REFERENCE_FILE )
    else()
      if ( ${_ARG} MATCHES NO_XML_OUTPUT )
	set ( _NO_XML_OUTPUT ON)
      elseif ( ${_ARG} MATCHES WILL_FAIL)
	set ( _WILL_FAIL ON)
      elseif ( ${_ARG} MATCHES ENVIRONMENT)
	set(_EXPECT_ADDITIONAL_ENVIRONMENT ON)
      elseif ( ${_ARG} MATCHES REFERENCE_FILE)
	set(_EXPECT_REFERENCE_FILE ON)
      else()
	message(FATAL_ERROR "test_crosseddelta2: unsupported option '${_ARG}'")
      endif()
    endif()
  endforeach(_ARG ${ARGN})
  if(_EXPECT_ADDITIONAL_ENVIRONMENT)
    message(FATAL_ERROR "test_crosseddelta2: expected environment variable")
  endif()
  if(_EXPECT_REFERENCE_FILE)
    message(FATAL_ERROR "test_crosseddelta2: expected reference file")
  endif()
  if(_REFERENCE_FILE)
    set(_REFERENCE_FILE "${PROJECT_SOURCE_DIR}/mfront/tests/behaviours/references/${_REFERENCE_FILE}")
  endif(_REFERENCE_FILE)
  set(test "crosseddelta2${test_arg}_mtest")
  set(file "${CMAKE_CURRENT_SOURCE_DIR}/crosseddelta2${test_arg}.mtest")
  set(_mtest_args)
  list(APPEND _mtest_args --verbose=level0)
  list(APPEND _mtest_args --result-file-output=false)
  list(APPEND _mtest_args --@interface@=umat)
  list(APPEND _mtest_args --@library@="$<TARGET_FILE:MFrontCastemBehaviours>")
  list(APPEND _mtest_args --@top_srcdir@=${PROJECT_SOURCE_DIR})
  list(APPEND _mtest_args --@mplibrary@="$<TARGET_FILE:MFrontMaterialProperties-castem>")
  list(APPEND _mtest_args --@reference_file@="${_REFERENCE_FILE}")
  if(_NO_XML_OUTPUT)
    list(APPEND _mtest_args --xml-output=false)
  else(_NO_XML_OUTPUT)
    list(APPEND _mtest_args --xml-output=true)
  endif(_NO_XML_OUTPUT)
  add_test(NAME ${test} COMMAND mtest ${_mtest_args} ${file})
  if(_WILL_FAIL)
    set_tests_properties(${test} PROPERTIES WILL_FAIL true)
  endif(_WILL_FAIL)
  if((CMAKE_HOST_WIN32) AND (NOT MSYS))
    set_property(TEST ${test}        
       PROPERTY ENVIRONMENT ${_ADDITIONAL_ENVIRONMENT}
      "PATH=$<TARGET_FILE_DIR:TFELMTest>\;$<TARGET_FILE_DIR:TFELMFront>\;$<TARGET_FILE_DIR:MFrontLogStream>\;$<TARGET_FILE_DIR:TFELMaterial>\;$<TARGET_FILE_DIR:TFELNUMODIS>\;$<TARGET_FILE_DIR:TFELMathParser>\;$<TARGET_FILE_DIR:TFELGlossary>\;$<TARGET_FILE_DIR:TFELSystem>\;$<TARGET_FILE_DIR:TFELUtilities>\;$<TARGET_FILE_DIR:TFELException>\;$<TARGET_FILE_DIR:TFELTests>\;$<TARGET_FILE_DIR:TFELConfig>\;$<TARGET_FILE_DIR:TFELUnicodeSupport>\;$ENV{PATH}")
  else((CMAKE_HOST_WIN32) AND (NOT MSYS))
    if(_ADDITIONAL_ENVIRONMENT)
      set_property(TEST ${test} PROPERTY ENVIRONMENT ${_ADDITIONAL_ENVIRONMENT})
    endif(_ADDITIONAL_ENVIRONMENT)
  endif((CMAKE_HOST_WIN32) AND (NOT MSYS))
  if(TFEL_APPEND_SUFFIX)
    install(FILES ${file}
      DESTINATION "share/doc/mfront-${TFEL_SUFFIX}/tests/behaviours/crosseddelta2"
      COMPONENT mtest)
  else(TFEL_APPEND_SUFFIX)
    install(FILES ${file}
      DESTINATION "share/doc/mfront/tests/behaviours/crosseddelta2"
      COMPONENT mtest)
  endif(TFEL_APPEND_SUFFIX)
endfunction(test_crosseddelta2)

test_crosseddelta2(ddif2-2)
test_crosseddelta2(ddif2-3)
test_crosseddelta2(ddif2-4)
test_crosseddelta2(ddif2)
test_crosseddelta2(druckerprager)
test_crosseddelta2(ellipticcreep)
test_crosseddelta2(finitestrainsinglecrystal-001)
test_crosseddelta2(finitestrainsinglecrystal-012-2)
test_crosseddelta2(finitestrainsinglecrystal-012)
test_crosseddelta2(finitestrainsinglecrystal-159)
test_crosseddelta2(finitestrainsinglecrystal2-001)
test_crosseddelta2(finitestrainsinglecrystal_numericaljacobian-001)
test_crosseddelta2(finitestrainsinglecrystal_numericaljacobian-012-2)
test_crosseddelta2(finitestrainsinglecrystal_numericaljacobian-012)
test_crosseddelta2(finitestrainsinglecrystal_numericaljacobian-159)
test_crosseddelta2(hayhurst)
test_crosseddelta2(implicithayhurst)
test_crosseddelta2(implicitmonocrystal)
test_crosseddelta2(implicitnorton3)
test_crosseddelta2(implicitnorton-planestress2)
test_crosseddelta2(implicitnorton-planestress)
test_crosseddelta2(lorentz)
test_crosseddelta2(monocrystal_dd_cc_irradiation)
test_crosseddelta2(monocrystal_dd_cc_irradiation_numericaljacobian)
test_crosseddelta2(monocrystal_dd_cfc_irradiation)
test_crosseddelta2(monocrystal_dd_cfc)
test_crosseddelta2(monocrystal_dd_cfc_numericaljacobian)
test_crosseddelta2(monocrystal)
test_crosseddelta2(norton2)
test_crosseddelta2(norton-euler)
test_crosseddelta2(norton)
test_crosseddelta2(norton-rk2)
test_crosseddelta2(nortonrk2)
test_crosseddelta2(nortonrk3)
test_crosseddelta2(norton-rk4)
test_crosseddelta2(nortonrk4)
test_crosseddelta2(plasisocin)
test_crosseddelta2(plasticity)
test_crosseddelta2(polycrystals_dd_cfc)
test_crosseddelta2(strainhardeningcreep)
test_crosseddelta2(stressrelaxation)
test_crosseddelta2(T91-viscositytest)
test_crosseddelta2(viscoplasisocin)

# test_crosseddelta2(mieheapellambrechtlogarithmicstrainnortoncreep-uniaxialloading-3-3D)
# test_crosseddelta2(monocrystal_dd_cc)
# test_crosseddelta2(monocrystal_dd_cc_numericaljacobian)
