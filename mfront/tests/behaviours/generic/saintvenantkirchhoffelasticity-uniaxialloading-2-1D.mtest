@Author Thomas Helfer;
@Date   27/07/2018;

@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStrain';
@XMLOutputFile @xml_output@;
@MaximumNumberOfSubSteps 1;
@Behaviour<generic> @library@ 'SaintVenantKirchhoffElasticity' {
  tangent_operator : @tangent_operator@
};

@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FRR' {0:1.,1.:1.2};
@ImposedDrivingVariable 'FZZ' 1.;
@ImposedDrivingVariable 'FTT' 1.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Real 'lambda' 'PoissonRatio*YoungModulus/((1+PoissonRatio)*(1-2*PoissonRatio))';
@Real 'mu'     'YoungModulus/(2*(1+PoissonRatio))';
@Test<function> {'SRR':'FRR*(lambda+2*mu)*(FRR**2-1)/2'} 1.e-2;
@Test<function> {'SZZ':'lambda*(FRR**2-1)/(2*FRR)'}      1.e-2;
@Test<function> {'STT':'lambda*(FRR**2-1)/(2*FRR)'}      1.e-2;
