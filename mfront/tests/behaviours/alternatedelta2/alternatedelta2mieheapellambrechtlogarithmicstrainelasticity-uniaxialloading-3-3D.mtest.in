/*! 
 * \file   castemmieheapellambrechtlogarithmicstrain-uniaxialloading-3-3D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  4 dec. 2013
 */

@AccelerationAlgorithm 'AlternateDelta2';
@Behaviour<umat> '@MFrontCastemBehavioursBuildPath@' 'umatlogarithmicstrainelasticity';
@StressEpsilon 1.e-4;
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FYY' {0:1.,1.:1.8};
@ImposedDrivingVariable 'FXY' 0.;
@ImposedDrivingVariable 'FYX' 0.;
@ImposedDrivingVariable 'FXZ' 0.;
@ImposedDrivingVariable 'FZX' 0.;
@ImposedDrivingVariable 'FYZ' 0.;
@ImposedDrivingVariable 'FZY' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Evolution<function> 'eyy' 'log(FYY**2)/2';
@Evolution<function> 'exx' '-PoissonRatio*eyy';
@Evolution<function> 'ezz' '-PoissonRatio*eyy';
@Evolution<function> 'J'   'sqrt(exp(2*exx)*exp(2*ezz))*FYY';
@Evolution<function> 'Tyy' 'YoungModulus*eyy';

@Test<function> {'SXX':'0','SZZ':'0'} 1.e-2;
@Test<function> {'SYY':'Tyy/J'}       1.e-2;
@Test<function> {'SXY':'0','SXZ':'0','SYZ':'0'}           1.e-2;
@Test<function> {'FXX': 'sqrt(exp(2*exx))','FZZ': 'sqrt(exp(2*ezz))'} 1.e-7;
@Test<function> {'FXY': '0', 'FZY':'0'} 1.e-10;
@Test<function> {'FXZ': '0', 'FZX':'0'} 1.e-10;
@Test<function> {'FYZ': '0', 'FZY':'0'} 1.e-10;



