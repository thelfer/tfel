@Parser    RungeKutta;
@Behaviour OrthotropicCreep;
@Author    Helfer Thomas;
@Algorithm rk54;
@Date      8/03/11;

@OrthotropicBehaviour;
@RequireStiffnessTensor;

@StateVariable Stensor evp; /* Viscoplastic strain            */
evp.setGlossaryName("ViscoplasticStrain");
@StateVariable real p;      /* Equivalent viscoplastic strain */
p.setGlossaryName("EquivalentViscoplasticStrain");

@AuxiliaryStateVariable real seq;
seq.setGlossaryName("HillStress");

@Parameter H_F = 0.371;
@Parameter H_G = 0.629;
@Parameter H_H = 4.052;
@Parameter H_L = 1.5;
@Parameter H_M = 1.5;
@Parameter H_N = 1.5;
@LocalVariable tfel::math::st2tost2<N,real> H;

@Includes{
#include<TFEL/Material/Lame.hxx>
#include<TFEL/Material/Hill.hxx>
}

@InitLocalVariables{
   H = hillTensor<N,real>(H_F,H_G,H_H,
			  H_L,H_M,H_N);
}

@ComputeStress{
  sig = D*eel;
}

@Derivative{
  const stress sigeq = sqrt(sig|H*sig);
  if(sigeq>1e9){
    return false;
  }
  Stensor  n(0.);
  if(sigeq > 10.e-7){
    n    = H*sig/sigeq;
  }
  dp   = 8.e-67*pow(sigeq,8.2);
  devp = dp*n;
  deel = deto - devp;
}

@UpdateAuxiliaryStateVars{
  seq = sqrt(sig|H*sig);
}
