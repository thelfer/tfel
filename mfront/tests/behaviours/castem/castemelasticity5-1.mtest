@Author T. Helfer;
@Date 26/02/2016;
@Description{
  "Test of the Elasticity5 behaviour which describes "
  "a simple isotropic elastic behaviour whose stiffness "
  "tensor is defined using the `@ComputeStiffnessTensor` "
  "keyword. The young modulus depends on the temperature."
};

@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStrain';
@AccelerationAlgorithm 'Cast3M';
@Behaviour<castem> @library@ 'umatelasticity5';

// those material properties are required by the catem interface but
// are not used by the behaviour
@MaterialProperty<constant> 'YoungModulus' 150.e9;
@MaterialProperty<constant> 'PoissonRatio'   0.3;

@ExternalStateVariable 'Temperature' {0:293.15,3600:800};
@ImposedStrain<function> 'ERR' '1.e-3*sin(t/900.)';

@Times {0.,3600 in 20};

// stresses
@Evolution<function> 'TC' 'Temperature-273.15';
@Evolution<function> 'E'  '(-3.1636e-3*TC*TC-3.8654*TC+2.1421e+4)*1e7';
@Real 'nu' 0.39991;

@Test<function> 'SRR' 'E*ERR' 1.e-3;
@Test<function> 'STT' '0.'    1.e-3;
@Test<function> 'SZZ' '0.'    1.e-3;
// strains
@Test<function> 'ETT' '-nu*ERR' 1.e-8;
@Test<function> 'EZZ' '-nu*ERR' 1.e-8;
