@Author Helfer Thomas;
@Date  09 avril 2013;
@Description{
  "This test checks that thermal expansion are correctly taken into "
  "account for an orthotropic material."
};

@PredictionPolicy 'ElasticPrediction';

@Behaviour<@interface@> @library@ 'umatorthotropicelastic';
@Real 'E'  100.e9;
@Real 'nu'   0.3;
@Real 'a'    2.e-5;
@MaterialProperty<constant> 'YoungModulus1'     'E';
@MaterialProperty<constant> 'YoungModulus2'     'E';
@MaterialProperty<constant> 'YoungModulus3'     'E';
@MaterialProperty<constant> 'PoissonRatio12'    'nu';
@MaterialProperty<constant> 'PoissonRatio13'    'nu';
@MaterialProperty<constant> 'PoissonRatio23'    'nu';
@MaterialProperty<constant> 'ShearModulus12'    100.e9;
@MaterialProperty<constant> 'ShearModulus13'     85.e9;
@MaterialProperty<constant> 'ShearModulus23'    120.e9;
@MaterialProperty<constant> 'ThermalExpansion1' 'a';
@MaterialProperty<constant> 'ThermalExpansion2' 'a';
@MaterialProperty<constant> 'ThermalExpansion3' 'a';

@ExternalStateVariable 'Temperature' {0:293.15,3600.:800};
@Real 'ThermalExpansionReferenceTemperature' 293.15;
@Real 'Tref' 'ThermalExpansionReferenceTemperature';

@ImposedStrain 'EXX' 0.;
@ImposedStrain 'EYY' 0.;
@ImposedStrain 'EZZ' 0.;

@Times {0.,3600 in 20};

@Test<function> 'SXX' '-E/(1-2*nu)*a*(Temperature-Tref)'  1.e-3;
@Test<function> 'SYY' '-E/(1-2*nu)*a*(Temperature-Tref)'  1.e-3;
@Test<function> 'SZZ' '-E/(1-2*nu)*a*(Temperature-Tref)'  1.e-3;
@Test<function> {'EXY':'0.','EXZ':'0.','EYZ':'0.'} 1.e-12;
