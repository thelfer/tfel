@Author Helfer Thomas;
@Date  29/02/2016;
@Description{
  "This test checks that the stiffness tensor "
  "is correctly computed for an "
  "orthotropic material."
  "The orthotropic behaviour uses the 'Pipe' "
  "orthotropic axes convention. This means that "
  "the direction 2 and 3 are interchanged "
  "with respect to three dimensional modelling."
};

@ModellingHypothesis 'PlaneStress';
@Behaviour<umat> @library@ 'umatorthotropicelastic6';

// Material properties for the three dimensional cases
@Real 'E1'   '7.8e+10';
@Real 'E2'   '2.64233e+11';
@Real 'E3'   '3.32e+11';
@Real 'nu12' '0.13';
@Real 'nu13' '0.18';
@Real 'nu23' '0.24';
@Real 'G12'  '4.8e+10';
@Real 'G13'  '7.8e+10';
@Real 'G23'  '1.16418e+11';

// Material properties taking into account that the the direction 2
// and 3 are inverchanged
@MaterialProperty<constant> 'YoungModulus1'     'E1';
@MaterialProperty<constant> 'YoungModulus2'     'E3';
@MaterialProperty<constant> 'YoungModulus3'     'E2';
@MaterialProperty<constant> 'PoissonRatio12'    'nu13';
@MaterialProperty<constant> 'PoissonRatio13'    'nu12';
@MaterialProperty<constant> 'PoissonRatio23'    'nu23*E3/E2';
@MaterialProperty<constant> 'ShearModulus12'    'G13';

@ExternalStateVariable 'Temperature' 293.15;
@Real 'ThermalExpansionReferenceTemperature' 293.15;
@Real 'Tref' 'ThermalExpansionReferenceTemperature';

@ImposedStrain<function> 'EYY' '1.e-3*sin(t/900.)';

@Times {0.,3600 in 20};

// stresses
@Test<function> 'SYY' 'E3*EYY'  1.e-3;
@Test<function> {'SZZ':'0','SXX':'0'}  1.e-3;
// strains
@Test<function> 'EXX' '-nu13*(E3/E1)*EYY'  1.e-12;
@Test<function> 'EXY' '0.' 1.e-12;
