/*! 
 * \file   castemelasticity_malls-uniaxialloading-2-2D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  4 dec. 2013
 */

@UseCastemAccelerationAlgorithm true;
@ModellingHypothesis 'Axisymmetrical';
@Behaviour<umat> @library@ 'umatelasticity_malls';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FTT' {0:1.,1.:1.8};
@ImposedDrivingVariable 'FRZ' 0.;
@ImposedDrivingVariable 'FZR' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Evolution<function> 'ett' 'log(FTT**2)/2';
@Evolution<function> 'err' '-PoissonRatio*ett';
@Evolution<function> 'ezz' '-PoissonRatio*ett';
@Evolution<function> 'J'   'sqrt(exp(2*err)*exp(2*ezz))*FTT';
@Evolution<function> 'Ttt' 'YoungModulus*ett';

@Test<function> {'SRR':'0','SZZ':'0'} 1.e-3;
@Test<function> {'STT':'Ttt/J'}       1.e-3;
@Test<function> {'SRZ':'0'}           1.e-4;
@Test<function> {'FRR': 'sqrt(exp(2*err))','FZZ': 'sqrt(exp(2*ezz))'} 1.e-10;
@Test<function> {'FRZ': '0', 'FZR':'0'} 1.e-10;



