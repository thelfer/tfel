@Author T. Helfer;
@Date 26/02/2016;
@Description{
  "Test of the Elasticity4 behaviour which describes "
  "a simple isotropic elastic behaviour whose stiffness "
  "tensor is defined using the `@ComputeStiffnessTensor` "
  "keyword."
};

@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStrain';
@AccelerationAlgorithm 'Cast3M';
@Behaviour<@interface@> @library@ 'umatelasticity4';

// those material properties are required by the catem interface but
// are not used by the behaviour
@MaterialProperty<constant> 'YoungModulus' 150.e9;
@MaterialProperty<constant> 'PoissonRatio'   0.3;

@ExternalStateVariable 'Temperature' 293.15;
@ImposedStrain<function> 'ERR' '1.e-3*sin(t/900.)';

@Times {0.,3600 in 20};

// stresses
@Real 'E'  7.8e+10;
@Real 'nu' 0.39991;
@Test<function> 'SRR' 'E*ERR' 1.e-3;
@Test<function> 'STT' '0.'    1.e-3;
@Test<function> 'SZZ' '0.'    1.e-3;
// strains
@Test<function> 'ETT' '-nu*ERR' 1.e-8;
@Test<function> 'EZZ' '-nu*ERR' 1.e-8;
