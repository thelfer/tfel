@Author Thomas Helfer;
@Date  20/09/2016;
@Description{
  Test of the generic plane stress support
  of the Cast3M interface for an
  isotropic behaviour.
};

@PredictionPolicy 'LinearPrediction';
@AccelerationAlgorithm 'Cast3M';
@ModellingHypothesis 'PlaneStress';
@Behaviour<umat> @library@ 'umatimplicitnorton4';
@MaterialProperty<constant> 'YoungModulus'     150.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@Real 'sxx' 20e6;
@ImposedStress 'SXX' 'sxx';
// Initial value of the elastic strain
@Real 'EELXX0' 0.00013333333333333333;
@Real 'EELZZ0' -0.00004;
@InternalStateVariable 'ElasticStrain' {'EELXX0','EELZZ0','EELZZ0',0.};
@InternalStateVariable 'AxialStrain' 'EELZZ0';
// Initial value of the total strain
@Strain {'EELXX0','EELZZ0',0,0};
// Initial value of the total stresses
@Stress {'sxx',0.,0.,0.};

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,3600 in 20};

// tests on strains
// note: EquivalentViscoplasticStrain is known at 1.e-12 (defaut value
// for @StrainEpsilon), thus we may expect the strain to be known at
// '3.6*1.e-9'. If pratice, things are a bit better but not much
// better.
@Real 'A' 8.e-67;
@Real 'E' 8.2;
@Test<function> 'EXX' 'EELXX0+A*SXX**E*t'     1.e-9;
@Test<function> 'EYY' 'EELZZ0-0.5*A*SXX**E*t' 1.e-10;
@Test<function> 'EZZ' '0' 1.e-10;
@Test<function> 'AxialStrain' 'EELZZ0-0.5*A*SXX**E*t' 1.e-10;
@Test<function> 'EXY' '0.'                    1.e-10;
// tests on internal state variables
@Test<function> 'ElasticStrainXX' 'EELXX0'  1.e-12;
@Test<function> 'ElasticStrainYY' 'EELZZ0'  1.e-12;
@Test<function> 'ElasticStrainZZ' 'EELZZ0'  1.e-12;
@Test<function> 'ElasticStrainXY' '0.'      1.e-12;
@Test<function> 'p'               'A*SXX**E*t' 3.e-11;
// this test is a bit paranoiac since SXX is imposed
@Test<function> 'SXX' 'SXX'     1.e-3;
// check that the mechanical equilibrium is satisfied
@Test<function> 'SYY' '0.'      1.e-3;
@Test<function> 'SZZ' '0.'      1.e-3;
@Test<function> 'SXY' '0.'      1.e-3;
