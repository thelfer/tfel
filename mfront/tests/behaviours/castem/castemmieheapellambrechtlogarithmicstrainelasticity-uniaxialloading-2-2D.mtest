/*! 
 * \file   castemmieheapellambrechtlogarithmicstrain-uniaxialloading-2-2D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  4 dec. 2013
 */

@ModellingHypothesis 'Axisymmetrical';
@UseCastemAccelerationAlgorithm true;
@Behaviour<umat> @library@ 'umatlogarithmicstrainelasticity';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FZZ' {0:1.,1.:1.8};
@ImposedDrivingVariable 'FRZ' 0.;
@ImposedDrivingVariable 'FZR' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Evolution<function> 'ezz' 'log(FZZ**2)/2';
@Evolution<function> 'err' '-PoissonRatio*ezz';
@Evolution<function> 'ett' '-PoissonRatio*ezz';
@Evolution<function> 'J'   'sqrt(exp(2*err)*exp(2*ett))*FZZ';
@Evolution<function> 'Tzz' 'YoungModulus*ezz';

@Test<function> {'SRR':'0','STT':'0'} 1.e-3;
@Test<function> {'SZZ':'Tzz/J'}       1.e-3;
@Test<function> {'SRZ':'0'}           1.e-4;
@Test<function> {'FRR': 'sqrt(exp(2*err))','FTT': 'sqrt(exp(2*ett))'} 1.e-10;
@Test<function> {'FRZ': '0', 'FZR':'0'} 1.e-10;



