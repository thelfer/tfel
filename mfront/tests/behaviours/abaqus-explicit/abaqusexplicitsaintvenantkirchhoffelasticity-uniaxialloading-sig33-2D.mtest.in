@Author T. Helfer;
@Date 15/09/2015;
@Description{
  "This test describes an uniaxial loading "
  "by imposing the deformation gradient in "
  "one direction. "
  "The perpendicular directions are "
  "stress-free. The shear components of the "
  "deformation gradient are set to zero. "
  "The results are compared to analytical solutions."
};

@ModellingHypothesis 'Axisymmetrical';
@Behaviour<abaqus_explicit> '@MFrontAbaqusExplicitBehavioursBuildPath@' 'SAINTVENANTKIRCHHOFFELASTICITY';
@MaterialProperty<constant> 'MassDensity'  15000.;
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FTT' {0:1.,1.:1.2};
@ImposedDrivingVariable 'FRZ' 0.;
@ImposedDrivingVariable 'FZR' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Test<function> {'STT':'YoungModulus*(FTT**2-1)*FTT/(2*(1-PoissonRatio*(FTT**2-1)))'} 1.e-2;
@Test<function> {'SRZ':'0'} 1.e-2;

@Test<function> {'FRR':'sqrt(1-PoissonRatio*(FTT**2-1))'} 1.e-8;
@Test<function> {'FZZ':'sqrt(1-PoissonRatio*(FTT**2-1))'} 1.e-8;

