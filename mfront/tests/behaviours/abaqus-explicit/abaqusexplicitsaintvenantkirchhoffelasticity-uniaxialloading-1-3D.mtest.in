/*! 
 * \file    SAINTVENANTKIRCHHOFFELASTICITY-uniaxialloading-1-3D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  26 nov. 2013
 */

@AccelerationAlgorithm 'Cast3M';
@Behaviour<abaqus_explicit> '@MFrontAbaqusExplicitBehavioursBuildPath@' 'SAINTVENANTKIRCHHOFFELASTICITY_3D';
@MaterialProperty<constant> 'MassDensity'  15000.;
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FXX' 1.;
@ImposedDrivingVariable 'FYY' {0:1.,1.:1.2};
@ImposedDrivingVariable 'FZZ' 1.;
@ImposedDrivingVariable 'FXY' 0.;
@ImposedDrivingVariable 'FYX' 0.;
@ImposedDrivingVariable 'FXZ' 0.;
@ImposedDrivingVariable 'FZX' 0.;
@ImposedDrivingVariable 'FYZ' 0.;
@ImposedDrivingVariable 'FZY' 0.;

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Real 'lambda' 'PoissonRatio*YoungModulus/((1+PoissonRatio)*(1-2*PoissonRatio))';
@Real 'mu'     'YoungModulus/(2*(1+PoissonRatio))';
@Test<function> {'SXX':'lambda*(FYY**2-1)/(2*FYY)'}      1.e-2;
@Test<function> {'SYY':'FYY*(lambda+2*mu)*(FYY**2-1)/2'} 1.e-2;
@Test<function> {'SZZ':'lambda*(FYY**2-1)/(2*FYY)'}      1.e-2;
@Test<function> {'SXY':'0','SXZ':'0','SYZ':'0'}          1.e-2;
@Test<function> {'InternalEnergy':'(lambda/2+mu)*(FYY**2-1)**2/4/MassDensity'} 1.e-7;
@Test<function> 'DissipatedEnergy' '0' 1.e-7;
