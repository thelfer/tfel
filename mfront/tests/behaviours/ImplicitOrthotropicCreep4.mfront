@DSL Implicit;
@Behaviour ImplicitOrthotropicCreep4;
@UseQt true;
@Author Helfer Thomas;
@Date 19 / 09 / 16;

@OrthotropicBehaviour<Pipe>;
@Brick   "StandardElasticity";
@Epsilon 1.e-12;

@ComputeStiffnessTensor<UnAltered>{young_modulus : 100e9, poisson_ratio : 0.3};
@HillTensor H{F : 0.371, G : 0.629, H : 4.052, L : 1.5, M : 1.5, N : 1.5};

@StateVariable strain p;
p.setGlossaryName("EquivalentViscoplasticStrain");

@AuxiliaryStateVariable stress seq;
seq.setGlossaryName("HillStress");

@Integrator{
  constexpr auto s0 = stress(100e6);
  constexpr auto de0 = strainrate(0.31848573644279365);
  constexpr auto E = real(8.2);
  const auto seps = real(1.e-12) * D(0, 0);
  seq = power<1, 2>(max(sig | H * sig, stress(0) * stress(0)));
  const auto rseq = seq / s0;
  const auto tmp = de0 * pow(rseq, E - 1.);
  const auto iseq = 1 / max(seq, seps);
  const auto n = (H * sig) * iseq;
  feel += dp * n;
  fp -= tmp * rseq * dt;
  // jacobian
  dfeel_ddeel += theta * dp * (H - (n ^ n)) * D * iseq;
  dfeel_ddp = n;
  dfp_ddeel = -theta * tmp * E * dt / s0 * (n | D);
}

@UpdateAuxiliaryStateVars{
  seq = power<1, 2>(max(sig | H_tdt * sig, stress(0) * stress(0)));
}
