/*! 
 * \file    castemfiniterotationsmallstrainelasticity2-shear-2-2D.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  26 nov. 2013
 */

@ModellingHypothesis 'GeneralisedPlaneStrain';
@Behaviour<umat> @library@ 'umatfiniterotationsmallstrainelasticity2';
@MaterialProperty<constant> 'YoungModulus'     215.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@ImposedDrivingVariable 'FXX' 1.;
@ImposedDrivingVariable 'FYY' 1.;
@ImposedDrivingVariable 'FZZ' 1.;
@ImposedDrivingVariable 'FXY' 0.;
@ImposedDrivingVariable 'FYX' {0:0.,1.:0.2};

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,1 in 10};

@Real 'lambda' 'PoissonRatio*YoungModulus/((1+PoissonRatio)*(1-2*PoissonRatio))';
@Real 'mu'     'YoungModulus/(2*(1+PoissonRatio))';
@Test<function> {'SXX':'((2*mu+lambda)*FYX**2)/2'} 1.e-2;
@Test<function> {'SYY':'((2*mu+lambda)*FYX**4+(4*mu+lambda)*FYX**2)/2'} 1.e-2;
@Test<function> {'SZZ':'(lambda*FYX**2)/2 '} 1.e-2;
@Test<function> {'SXY':'sqrt(2)*((2*mu+lambda)*FYX**3+2*mu*FYX)/2'} 1.e-2;

