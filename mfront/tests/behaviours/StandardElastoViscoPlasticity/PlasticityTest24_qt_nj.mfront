@DSL Implicit;
@Behaviour PlasticityTest24_qt_nj;
@Author Thomas Helfer;
@Date 03 / 12 / 2024;
@UseQt true;

@Algorithm NewtonRaphson_NumericalJacobian;
@PerturbationValueForNumericalJacobianComputation 1e-8;
@Epsilon 1.e-14;
@Theta 1;

@Parameter real alpha = 0.62;
alpha.setEntryName("ProportionOfIsotropicHardeningRules");

@Parameter stress Ks = 1115.15e6;
Ks.setEntryName("SwiftCoefficient");
@Parameter strain p0s = 0.0013;
p0s.setEntryName("SwiftStrainShift");
@Parameter real ns = 0.31;
ns.setEntryName("SwiftExponent");

@Parameter stress Q1 = 1050.3e6;
Q1.setEntryName("VoceUltimateStrength");
@Parameter real Q2 = 0.445;
Q2.setEntryName("SecondVoceParameter");
@Parameter real Q3 = 13.06;
Q3.setEntryName("ThirdVoceParameter");

@Parameter real A = 0.0362;
A.setEntryName("JohnsonCookCoefficient");
@Parameter strainrate dp0 = 1e-3;
dp0.setEntryName("JohnsonCookReferenceStrainRate");

@Brick StandardElastoViscoPlasticity{
  stress_potential : "Hooke" {young_modulus : 210e9, poisson_ratio : 0.3},
  inelastic_flow : "Plastic" {
    criterion : "Mises",
    isotropic_hardening : "StrainRateSensitive" {
      rate_independent_isotropic_hardening :
          "Swift" {R0 : "alpha * Ks * (p0s ** ns)", p0 : "p0s", n : "ns"},
      rate_independent_isotropic_hardening : "Voce" {
        R0 : "(1 - alpha) * Q1 * (1 - Q2)",
        Rinf : "(1 - alpha) * Q1",
        b : "Q3"
      },
      rate_sensitivity_factor : "JohnsonCook" {A : "A", dp0 : "dp0"}
    }
  }
};
