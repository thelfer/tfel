/*! 
 * \file   castemimplicitnorton-planestress.mtest
 * \brief
 * \author Helfer Thomas
 * \brief  09 avril 2013
 */

@Description{
  "Test the generic plane stress"
  "algorithm of the umat interface."
  "This is much less efficient than"
  "directly supporting plane stress"
  "within the behaviour."
 };

@MaximumNumberOfSubSteps   1;
@AccelerationAlgorithm 'CrossedSecant';

@ModellingHypothesis 'PlaneStress';
@Behaviour<umat> '@MFrontCastemBehavioursBuildPath@' 'umatimplicitnorton2';

@MaterialProperty<constant> 'YoungModulus'     150.e9;
@MaterialProperty<constant> 'PoissonRatio'       0.3;

@Real 'srr' 20.e6;
@ImposedStress 'SXX' 'srr';
// Initial value of the elastic strain
@Real 'EELXX0' 0.00013333333333333333;
@Real 'EELZZ0' -0.00004;
@InternalStateVariable 'ElasticStrain' {'EELXX0','EELZZ0','EELZZ0',0.};
@InternalStateVariable 'AxialStrain'   'EELZZ0';
// Initial value of the total strain
@Strain {'EELXX0','EELZZ0',0.,0.};
// Initial value of the stresses
@Stress {'srr',0.,0.,0.};

@ExternalStateVariable 'Temperature' 293.15;

@Times {0.,3600 in 20};

// tests on strains
// note: p is known at 1.e-12 (defaut value
// for @StrainEpsilon), thus we may expect the strain to be known at
// '3.6*1.e-9'. If pratice, things are a bit better but not much
// better.
@Real 'A' 8.e-67;
@Real 'E' 8.2;
@Test<function> 'EXX' 'EELXX0+A*SXX**E*t'     1.e-9;
@Test<function> 'EYY' 'EELZZ0-0.5*A*SXX**E*t' 1.e-10;
@Test<function> 'EZZ' '0.'                    1.e-10;
@Test<function> 'AxialStrain' 'EELZZ0-0.5*A*SXX**E*t' 1.e-10;
@Test<function> 'EXY' '0.'                    1.e-10;
// tests on internal state variables
@Test<function> 'ElasticStrainXX' 'EELXX0'  1.e-12;
@Test<function> 'ElasticStrainYY' 'EELZZ0'  1.e-12;
@Test<function> 'ElasticStrainZZ' 'EELZZ0'  1.e-12;
@Test<function> 'ElasticStrainXY' '0.'      1.e-12;
@Test<function> 'EquivalentViscoplasticStrain[0]' 'A*SXX**E*t/2' 1.e-12;
@Test<function> 'EquivalentViscoplasticStrain[1]' 'A*SXX**E*t/2' 1.e-12;
// this test is a bit paranoiac since SXX is imposed
@Test<function> 'SXX' 'SXX'     1.e-3;
// check that the mechanical equilibrium is satisfied
@Test<function> 'SZZ' '0.'      1.e-3;
@Test<function> 'SYY' '0.'      1.e-3;
@Test<function> 'SXY' '0.'      1.e-3;
