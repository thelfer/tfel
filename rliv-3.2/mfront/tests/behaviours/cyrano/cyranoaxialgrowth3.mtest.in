@Author Helfer Thomas;
@Date   08 ao√ªt 2016;
@Description{
  "This test checks that @AxialGrowth keyword is correctly taken into "
  "account for an orthotropic material."
};

@ModellingHypothesis 'AxisymmetricalGeneralisedPlaneStrain';
@Behaviour<cyrano> '@MFrontCyranoBehavioursBuildPath@' 'cyranoaxialgrowth2';

@Real 'E1'   '7.8e+10';
@Real 'E2'   '2.64233e+11';
@Real 'E3'   '2.64233e+11';
@Real 'nu12' '0.0039991';
@Real 'nu13' '0.0039991';
@Real 'nu23' '0.00736051';

@MaterialProperty<constant> 'YoungModulus1'     'E1';
@MaterialProperty<constant> 'YoungModulus2'     'E2';
@MaterialProperty<constant> 'YoungModulus3'     'E3';
@MaterialProperty<constant> 'PoissonRatio12'    'nu12';
@MaterialProperty<constant> 'PoissonRatio13'    'nu13';
@MaterialProperty<constant> 'PoissonRatio23'    'nu23';

@Real 'tmax' '3600*24*365';
@ExternalStateVariable 'Temperature' 293.15;
@ExternalStateVariable 'FastNeutronFluence_1MeV' {0:0,'tmax':3e25};
@Evolution<function> 'ag' '2.1224e-4*(FastNeutronFluence_1MeV/1e25)**0.794';
// transverse growth
@Evolution<function> 'tg' '1/sqrt(1+ag)-1';

@ImposedStrain 'ERR' 0.;
@ImposedStrain 'EZZ' 0.;
@ImposedStrain 'ETT' 0.;

@Times {0.,'tmax' in 20};

@Real 'S11' '1/E1';
@Real 'S22' '1/E2';
@Real 'S33' '1/E3';
@Real 'S12' '-nu12/E1';
@Real 'S13' '-nu13/E1';
@Real 'S23' '-nu23/E2';
@Real 'dS'  'S11*S22*S33+2*S23*S13*S12-S11*S23*S23-S22*S13*S13-S33*S12*S12';
@Real 'C00' '(S22*S33-S23*S23)/dS';
@Real 'C01' '(S13*S23-S12*S33)/dS';
@Real 'C02' '(S12*S23-S13*S22)/dS';
@Real 'C10' '(S13*S23-S12*S33)/dS';
@Real 'C11' '(S11*S33-S13*S13)/dS';
@Real 'C12' '(S12*S13-S11*S23)/dS';
@Real 'C20' '(S12*S23-S13*S22)/dS';
@Real 'C21' '(S12*S13-S11*S23)/dS';
@Real 'C22' '(S11*S22-S12*S12)/dS';
@Test<function> 'SRR' '-((C00+C02)*tg+C01*ag)'  1.e-3;
@Test<function> 'SZZ' '-((C10+C12)*tg+C11*ag)'  1.e-3;
@Test<function> 'STT' '-((C20+C22)*tg+C21*ag)'  1.e-3;

